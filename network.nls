
;;
; network evolution


to network:evolve-network
  
      
    ; grow the network
    network:grow-physical-network
     
    ; update distance matrices
    network:update-distance-matrices
  
    ; put distance matrix in history
    ;show (word "avg distmat = " (mean (map mean matrix:to-row-list distance-matrix)))
    table:put global:distance-matrices (item ticks global:dates) matrix:copy global:distance-matrix
    
    ; update interaction weights
    cities:update-weights
    
  
end




;;
; grow physical network
to network:grow-physical-network
  
    ; evolve speeds (i.e. impedances) following a slime mould heuristic
    ;  flows already computed in population (for feedbak if needed)
    ; no new links in this heuristic
    
    ;; thresholded link growth as a function of flow
    ;if global:slime-mould-reinforcment-function = "thresholded" [
    ;  set global:network-reinforcment-threshold quantile [path:flow] of links with [path:flow > 0] global:network-reinforcment-quantile
    ;
    ;  ask paths [
    ;    set path:travel-time path:travel-time * (1 + (global:network-reinforcment-gmax * (global:network-reinforcment-threshold ^ global:network-reinforcment-exponent - path:flow ^ global:network-reinforcment-exponent) / (global:network-reinforcment-threshold ^ global:network-reinforcment-exponent + path:flow ^ global:network-reinforcment-exponent)))
    ;    set path:pace path:travel-time / path:length
    ;  ]
    ;]
  
  ;;
  ; evolve link speeds following investments by governments
    
 
  
end


to network:update-distance-matrices
  ; recompute nw measures (needed twice)
  network:cache-nw-measures
  
  ask cities [
    ask other cities [
      ; FIXME this is just a copy of the distance matric - could directly use travel time matrix ? - unconvenient dual implementation
      matrix:set global:distance-matrix city:index [city:index] of myself (table:get global:nw-travel-times (list city:index [city:index] of myself))
    ]
  ]
end



;;
; flow mat -> virtual links
to network:update-pair-flows
  ask pair-paths [set pair-path:flow matrix:get global:gravity-flows ([city:index] of end1) ([city:index] of end2)]
end


;;
; show/hide direct flow links
to network:toggle-pair-flows
  set global:pair-paths-hidden? not global:pair-paths-hidden?
end


;; 
;  network indicators
to network:cache-nw-measures
  
  ; caching shortest paths and measures
  nw:set-context turtles paths
  ask paths [
    set path:bw-centrality 0
    set path:flow 0
  ]
  ask cities [set city:bw-centrality 0 set city:flow 0]
  set global:shortest-paths table:make set global:nw-speeds table:make set global:nw-travel-times table:make set global:nw-detours table:make set global:pairs-total-weight 0
  let clist to-list cities let n count cities
  let i 0 let j 1
  let ltot 0
  repeat (length clist - 1) [
    let c1 item i clist
    set j i + 1
    repeat (length clist - i - 1) [
      let c2 item j clist
      ; get current real flow
      let current-flow matrix:get global:gravity-flows [city:index] of c1 [city:index] of c2
      
      ;let current-feedback-flow matrix:get global:feedback-flows [city:index] of c1 [city:index] of c2
      
      ; compute shortest path
      let currentpath [] let tpath [] ask c1 [set currentpath nw:weighted-path-to c2 "path:travel-time" set tpath nw:turtles-on-weighted-path-to c2 "path:travel-time"]
      ; update link betweeness
      let l 0 let t 0 foreach currentpath [
        ? -> ask ? [
          set t t + path:travel-time
          set l l + path:length
          set path:bw-centrality path:bw-centrality + 1
          set path:flow path:flow + (current-flow * 2 / (n * (n - 1)))
          ;set path:feedback-flow path:feedback-flow + (current-feedback-flow * 2 / (n * (n - 1))) ; disactivated
        ]
      ]
      ; update node betweeness
      ;foreach tpath [ask ? [set city-bw-centrality city-bw-centrality + 1 set city-flow city-flow + (current-flow * 2 / (n * (n - 1)))]]
      ; update shortest path table
      table:put global:shortest-paths (list [city:index] of c1 [city:index] of c2) currentpath
      table:put global:shortest-paths (list [city:index] of c2 [city:index] of c1) (rev currentpath)
      ; update nw distance table
      table:put global:nw-travel-times (list [city:index] of c1 [city:index] of c2) t
      table:put global:nw-travel-times (list [city:index] of c2 [city:index] of c1) t
      ; update relative speed table
      let deucl 0 ask c1 [set deucl distance c2]
      table:put global:nw-speeds (list [city:index] of c1 [city:index] of c2) (deucl / t)
      table:put global:nw-detours (list [city:index] of c1 [city:index] of c2) (deucl / l)
      
      set global:pairs-total-weight global:pairs-total-weight + [city:population] of c1 + [city:population] of c2
      set j j + 1
      set ltot ltot + l
    ]
    set i i + 1
  ]
  show (word "avg distance cities = " (2 * ltot / (n * (n - 1))))
  
end


;;
; connect cities to the network
to network:connect-cities
  ask cities [
    if count my-paths = 0 [create-path-with one-of nodes with-min [distance myself]]
  ]
end






to node:new-node
  set hidden? true
end



to path:new-path
  set path:length link-length 
  ifelse path:initial-pace = 0 [
    set path:pace 1
  ][
    set path:pace path:initial-pace
  ]
  set path:travel-time path:length * path:pace
  set path:flow 0
  set hidden? false
end







